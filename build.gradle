plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "org.springframework.boot" version "2.3.2.RELEASE"
    id "com.jfrog.bintray" version "1.8.5"
    id "maven-publish"
}

repositories {
    jcenter()

    maven {
        url = "https://dl.bintray.com/jhonnold/maven"
    }
}

dependencies {
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect"

    implementation group: "me.honnold", name: "mpq", version: "1.0.1"
    implementation group: "me.honnold", name: "s2protocol", version: "1.0.4"

    implementation group: "org.springframework.boot", name: "spring-boot-starter-webflux", version: "2.3.2.RELEASE"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-security", version: "2.3.2.RELEASE"
    implementation group: "org.springframework.boot", name: "spring-boot-starter-data-r2dbc", version: "2.3.2.RELEASE"
    annotationProcessor group: "org.springframework.boot", name: "spring-boot-configuration-processor", version: "2.3.2.RELEASE"

    implementation group: "org.flywaydb", name: "flyway-core", version: "6.5.2"
    runtimeOnly group: "org.postgresql", name: "postgresql", version: "42.2.14"
    runtimeOnly group: "io.r2dbc", name: "r2dbc-postgresql", version: "0.8.4.RELEASE"

    implementation group: "software.amazon.awssdk", name: "s3", version: "2.13.63"
    implementation group: "software.amazon.awssdk", name: "netty-nio-client", version: "2.13.63"

    implementation group: "io.jsonwebtoken", name: "jjwt-api", version: "0.11.2"
    runtimeOnly group: "io.jsonwebtoken", name: "jjwt-impl", version: "0.11.2"
    runtimeOnly group: "io.jsonwebtoken", name: "jjwt-jackson", version: "0.11.2"

    implementation group: "io.projectreactor.kotlin", name: "reactor-kotlin-extensions", version: "1.0.2.RELEASE"
    implementation group: "org.apache.commons", name: "commons-text", version: "1.8"
    implementation group: "org.apache.commons", name: "commons-compress", version: "1.20"
    implementation group: "com.github.slugify", name: "slugify", version: "2.4"
    implementation group: "com.googlecode.json-simple", name: "json-simple", version: "1.1.1"


}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

group "me.honnold"
version "0.1.0"

configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf { it.buildDependencies.getDependencies(null).contains(jar) }
        it.outgoing.artifact(bootJar)
    }
}

publishing {
    publications {
        ladderhero(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GithubPackages"
            url = "https://maven.pkg.github.com/jhonnold/ladder-hero"

            credentials {
                username = project.hasProperty("github.user") ? project.property("github.user") : System.getenv("GITHUB_USER")
                password = project.hasProperty("github.key") ? project.property("github.key") : System.getenv("GITHUB_KEY")
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintray.user") ? project.property("bintray.user") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("bintray.key") ? project.property("bintray.key") : System.getenv("BINTRAY_KEY")
    publications = ["ladderhero"]

    pkg {
        repo = "maven"
        name = "ladder-hero"
    }
}